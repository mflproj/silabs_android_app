/*
 * Copyright Â© 2019 Silicon Labs, http://www.silabs.com. All rights reserved.
 */

def versionMajor = 3
def versionMinor = 0
def versionPatch = 1
def buildNumber = 37

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
kapt {
    generateStubs = true
}

buildscript {
    ext.kotlin_version = '1.3.72'
    ext.dagger_version = '2.21'
    repositories {
        maven { url 'https://maven.google.com' }
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'org.ajoberstar:grgit:1.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext {
    try {
        grgit = Grgit.open(dir: '.')
        gitSha = grgit.head().abbreviatedId
    } catch (ignored) {
        gitSha = ""
    }
    projectName = "BTMeshApp"

    sourceCodeExists = new File('../ble_mesh-android_api').exists()
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.siliconlabs.bluetoothmesh"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + buildNumber
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "Integer", "BUILD_NUMBER", "${buildNumber}"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        debug {
            debuggable true
        }
    }

    flavorDimensions "lib"
    productFlavors {
        aar {
            dimension "lib"
        }
        if (sourceCodeExists) {
            sourceCode {
                dimension "lib"
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${projectName}-${versionName}-${buildNumber}"
            if (gitSha) {
                outputFileName += "-${gitSha}"
            }
            outputFileName += ".apk"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'junit:junit:4.12'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.documentfile:documentfile:1.0.1'

    // ble_mesh
    aarImplementation project(':ble_mesh-android_api_high-release')
    aarImplementation project(':ble_mesh-android_api_low-release')
    if (sourceCodeExists) {
        sourceCodeImplementation project(":ble_mesh-android_api_high")
        sourceCodeImplementation project(":ble_mesh-android_api_low")
    }
    //RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'com.jakewharton.rx2:replaying-share:2.1.1'
    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    // View
    implementation "com.daimajia.swipelayout:library:1.2.0@aar"
    //Test
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC1'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.mockito:mockito-inline:2.8.47'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
